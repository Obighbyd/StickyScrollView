apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

def libName = 'stickyscrollview'
def libGroup = 'com.github.nitrico.' + libName
def libSite = 'https://github.com/nitrico/' + libName
def libGit = libSite + '.git'
def libTracker = libSite + '/issues'
def libDesc = 'Sticky observable nested ScrollView for Android'
def libTags = ['android', 'widget', 'sticky', 'observable', 'scrollview', 'nestedscrollview']

group = libGroup
version = '1.0.2'

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty('bintray.user')
    key = properties.getProperty('bintray.key')
    configurations = ['archives']
    pkg {
        repo = 'maven'
        name = libName
        desc = libDesc
        websiteUrl = libSite
        issueTrackerUrl = libTracker
        vcsUrl = libGit
        licenses = ['Apache-2.0']
        labels = libTags
        publish = true
        publicDownloadNumbers = true
    }
}

install {
    repositories.mavenInstaller {
        pom.project {
            packaging 'aar'
            groupId libGroup
            artifactId libName
            name libName
            description libDesc
            url libSite
            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                }
            }
            developers {
                developer {
                    id 'moreno'
                    name 'Miguel √Ångel Moreno'
                    email 'nitrico@gmail.com'
                }
            }
            scm {
                connection libGit
                developerConnection libGit
                url libSite
            }
        }
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

artifacts {
    archives sourcesJar
}
